//hedgehog-engine build.gradle

buildscript {
  ext {
    springBootVersion = '1.5.4.RELEASE'
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath('org.springframework:springloaded:1.2.6.RELEASE')
  }
}

repositories {
  mavenCentral()
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("${buildDir}/classes/main/")
    }
}

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8


dependencies {
  //compile('org.springframework.boot:spring-boot-starter-data-jpa')
  compile('org.springframework.boot:spring-boot-starter-web')
  //compile("com.h2database:h2")

  runtime('org.springframework.boot:spring-boot-devtools')

  testCompile(
          'org.springframework.boot:spring-boot-starter-test',
          'junit:junit:4.12',
          'org.codehaus.groovy:groovy-all:2.4.4',
          'org.spockframework:spock-core:1.0-groovy-2.4',
          'org.spockframework:spock-spring:1.0-groovy-2.4',

          // optional dependencies for using Spock
          "org.hamcrest:hamcrest-core:1.3", // only necessary if Hamcrest matchers are used
          "net.bytebuddy:byte-buddy:1.6.5", // allows mocking of classes (in addition to interfaces)
          "org.objenesis:objenesis:2.5.1", // allows mocking of classes without default constructor (together with CGLIB)
  )
}

jar {
  from('../hedgehog-webapp/dist') {
    //Public is a default supported Spring Boot resources directory.
    into 'public'
  }
}

//frontend:build will be run before the processResources
processResources.dependsOn(':hedgehog-webapp:build')

jacocoTestReport {
  reports {
    xml.enabled true
  }
}

check.dependsOn jacocoTestReport
